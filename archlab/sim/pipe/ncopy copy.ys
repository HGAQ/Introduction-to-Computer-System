#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# 刘松瑞 2100011819
# Describe how and why you modified the baseline code.
# 1. 使用iaddq代替addq，避免多余的内存引用
# 2. 九重循环展开
# 3. 三叉树处理余数
# 4. 去掉开头xor
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	# irmovq $1, %rcx
	# irmovq $8, %r8
	# xorq %rax,%rax		# count = 0;
	iaddq $-9, %rdx		# len -	9
	andq %rdx,%rdx		# len <= 9?
	jle Rest			# goto rest
Round1:	
	mrmovq (%rdi)  , %r8	# read val from src...
	mrmovq 8(%rdi) , %r9
	mrmovq 16(%rdi), %r10
	mrmovq 24(%rdi), %r11
	mrmovq 32(%rdi), %r12
	mrmovq 40(%rdi), %r13
	mrmovq 48(%rdi), %r14
	mrmovq 56(%rdi), %rcx
	mrmovq 64(%rdi), %rbx
	
	andq %r8, %r8			# val <= 0....
	rmmovq %r8, (%rsi)		# ...and store it to dst
	jle Round2
	iaddq $1, %rax		# ...count++
Round2:
	rmmovq %r9, 8(%rsi)	# ...and store it to dst
	andq %r9, %r9			# val <= 0....
	jle Round3
	iaddq $1, %rax		# ...count++
Round3:
	rmmovq %r10, 16(%rsi)	# ...and store it to dst
	andq %r10, %r10			# val <= 0....
	jle Round4
	iaddq $1, %rax		# ...count++
Round4:
	rmmovq %r11, 24(%rsi)	# ...and store it to dst
	andq %r11, %r11			# val <= 0....
	jle Round5
	iaddq $1, %rax		# ...count++
Round5:
	rmmovq %r12, 32(%rsi)	# ...and store it to dst
	andq %r12, %r12			# val <= 0....
	jle Round6
	iaddq $1, %rax		# ...count++
Round6:
	rmmovq %r13, 40(%rsi)	# ...and store it to dst
	andq %r13, %r13			# val <= 0....
	jle Round7
	iaddq $1, %rax		# ...count++
Round7:
	rmmovq %r14, 48(%rsi)	# ...and store it to dst
	andq %r14, %r14		# val <= 0....
	jle Round8
	iaddq $1, %rax		# ...count++
Round8:
	rmmovq %rcx, 56(%rsi)	# ...and store it to dst
	andq %rcx, %rcx			# val <= 0....
	jle Round9
	iaddq $1, %rax		# ...count++
Round9:
	rmmovq %rbx, 64(%rsi)	# ...and store it to dst
	andq %rbx, %rbx			# val <= 0....
	jle Npos
	iaddq $1, %rax		# ...count++
Npos:	
	iaddq $-9, %rdx		# len--
	iaddq $72, %rdi		# src++
	iaddq $72, %rsi		# dst++
	andq %rdx, %rdx		# len > 0?
	jg Round1			# if so, goto Round1:

Rest: 
# len - 9 <= 0
	iaddq $6, %rdx
	jg Big 				#	len - 3 > 0 
	jl Small 			#	len - 3 < 0
	je Loop3			#	len == 3
Small:
	iaddq $2, %rdx		#	len - 1  
	je Loop1			#	len == 1
	iaddq $-1, %rdx
	je Loop2 			# 	len == 2
	jmp Done				#   len == 0
Big:
	iaddq $-3, %rdx		#	len - 6 ? 
	jg Bigbig           #   len == 7,8,9
	jl Bigsmall			#   len == 4,5
	je Loop6			# 	len == 6
Bigbig:
	iaddq $-2, %rdx 	# 	len - 8
	jl Loop7			#	len == 7
	je Loop8			#	len == 8
	jg Loop9			#   len == 9
Bigsmall:
	iaddq $1, %rdx 		# 	len - 5
	jl Loop4			#   len == 4
	je Loop5			#	len == 5


Loop9:
	mrmovq 64(%rdi), %r10
	rmmovq %r10, 64(%rsi)
	andq %r10,%r10
Loop8:
	mrmovq 56(%rdi), %r11
	jle Loop8_
	iaddq $1, %rax		# ...count++
Loop8_:
	rmmovq %r11, 56(%rsi)
	andq %r11,%r11
Loop7:
	mrmovq 48(%rdi), %r10
	jle Loop7_
	iaddq $1, %rax		# ...count++
Loop7_:
	rmmovq %r10, 48(%rsi)
	andq %r10,%r10
Loop6:
	mrmovq 40(%rdi), %r10
	jle Loop6_
	iaddq $1, %rax		# ...count++
Loop6_:
	rmmovq %r10, 40(%rsi)
	andq %r10,%r10
Loop5:
	mrmovq 32(%rdi), %r10
	jle Loop5_
	iaddq $1, %rax		# ...count++
Loop5_:
	rmmovq %r10, 32(%rsi)
	andq %r10,%r10
Loop4:
	mrmovq 24(%rdi), %r10
	jle Loop4_
	iaddq $1, %rax		# ...count++
Loop4_:
	rmmovq %r10, 24(%rsi)
	andq %r10,%r10
Loop3:
	mrmovq 16(%rdi), %r10
	jle Loop3_
	iaddq $1, %rax		# ...count++
Loop3_:
	rmmovq %r10, 16(%rsi)
	andq %r10,%r10
Loop2:
	mrmovq 8(%rdi), %r10
	jle Loop2_
	iaddq $1, %rax		# ...count++
Loop2_:
	rmmovq %r10, 8(%rsi)
	andq %r10,%r10
Loop1:
	mrmovq (%rdi), %r10
	jle Loop1_
	iaddq $1, %rax		# ...count++
Loop1_:
	rmmovq %r10, (%rsi)
	andq %r10,%r10
	jle Done
	iaddq $1, %rax		# ...count++


	
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
